################################################################################
#
# RevBayes Example: Bayesian inference of phylogeny using a GTR-Covariotide
#                   substitution model on a single gene.
#
# authors: Sebastian Hoehna
#
################################################################################


### Read in sequence data for the gene
data_dna = readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")
data = data_dna.expandCharacters(2)

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()


mvi = 1
mni = 1


######################
# Substitution Model #
######################

Q_JC <- fnJC(4)


# specify the stationary frequency parameters
pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)
moves[mvi++] = mvBetaSimplex(pi, weight=2.0)
moves[mvi++] = mvDirichletSimplex(pi, weight=1.0)


# specify the exchangeability rate parameters
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves[mvi++] = mvBetaSimplex(er, weight=3.0)
moves[mvi++] = mvDirichletSimplex(er, weight=1.5)


# create a deterministic variable for the rate matrix, GTR
Q_GTR := fnGTR(er,pi) 

lambda ~ dnExp(1.0)
moves[mvi++] = mvScale(lambda)

Q := fnCovarion(RateMatrices=[Q_JC, Q_GTR],
                RateScalars=[0.0,1.0],
                SwitchRates=[[lambda,lambda,lambda,lambda],[lambda,lambda,lambda,lambda]],
                rescaled=TRUE)


##############
# Tree model #
##############

out_group = clade("Galeopterus_variegatus")

# Prior distribution on the tree topology
br_len_lambda <- 10.0
psi ~ dnUniformTopologyBranchLength(taxa, branchLengthDistribution=dnExponential(br_len_lambda), outgroup=out_group)

moves[mvi++] = mvNNI(psi, weight=num_taxa)
moves[mvi++] = mvSPR(psi, weight=num_taxa/10.0)
moves[mvi++] = mvBranchLengthScale(psi, weight=num_branches)

TL := psi.treeLength()



###################
# PhyloCTMC Model #
###################

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="NaturalNumbers")

# attach the data
seq.clamp(data)


############
# Analysis #
############

mymodel = model(psi)

# add monitors
monitors[mni++] = mnScreen(TL, printgen=1000)
monitors[mni++] = mnFile(psi, filename="output/primates_cytb_covariotide.trees", printgen=10)
monitors[mni++] = mnModel(filename="output/primates_cytb_covariotide.log", printgen=10)

# run the analysis
mymcmc = mcmc(mymodel, moves, monitors, nruns=2, combine="mixed")
mymcmc.run(generations=20000,tuningInterval=200)


# summarize output
treetrace = readTreeTrace("output/primates_cytb_covariotide.trees", treetype="non-clock", outgroup=out_group)
# and then get the MAP tree
map_tree = mapTree(treetrace,"output/primates_cytb_covariotide_MAP.tre")


# you may want to quit RevBayes now
q()

