################################################################################
#
# RevBayes Analysis: Bayesian inference of diversification rates under a
#                    character-dependent birth-death model.
#                    Here each transition rate between observed states is
#                    drawn from an independent exponentially distributed 
#                    rate. The transition rates between hidden states are
#                    all equal and drawn from another exponentially 
#                    distributed rate.
# 
#
# authors: Sebastian Hoehna, Will Freyman
#
################################################################################

#######################
# Reading in the Data #
#######################



# Global parameters for our analysis
NUM_TOTAL_SPECIES     = 367
NUM_STATES            = 2
NUM_HIDDEN            = 4
NUM_RATES             = NUM_STATES * NUM_HIDDEN
H                     = 0.587405
DATASET               = "activity_period"

# Read in the observed phylogeny
observed_phylogeny <- readTrees("data/primates_tree.nex")[1]
data <- readCharacterData("data/primates_"+DATASET+".nex")

# we need to expand the data to the hidden states
data_exp <- data.expandCharacters( NUM_HIDDEN )

# Get the names of the taxa in the tree and the age of the tree. We need these later on.
taxa <- observed_phylogeny.taxa()
tree_length <- observed_phylogeny.treeLength()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()


####################
# Create the rates #
####################

### Specify a prior on the speciation and extinction rates
rate_mean <- (NUM_TOTAL_SPECIES-2) / tree_length



### First, we create the hidden speciation rates
# We fix the (log) mean of the hidden speciation rate to 1.0, so that these rates are relative
ln_speciation_hidden_mean <- ln(1.0)

# We draw the standard deviation of the hidden rates from an exponential distribution with mean H 
# (so that we expect the 95% interval of the hidden speciation rate to span 1 order of magnitude).
speciation_hidden_sd ~ dnExponential( 1.0 / H )
moves.append( mvScale(speciation_hidden_sd, lambda=1, tune=true, weight=2.0) )

# Create a deterministic variable for the hidden speciation rate categories
# using a discretized lognormal distribution (the N-quantiles of it)
speciation_hidden_unormalized := fnDiscretizeDistribution( dnLognormal(ln_speciation_hidden_mean, speciation_hidden_sd), NUM_HIDDEN )

# We normalize the hidden speciation rates by dividing the rates with the main 
# (so the mean of the normalized rates equals to 1.0)
speciation_hidden := speciation_hidden_unormalized / mean(speciation_hidden_unormalized)



### Second, we create the hidden extinction rates
# We fix the (log) mean of the hidden extinction rate to 1.0, so that these rates are relative
ln_extinction_hidden_mean <- ln(1.0)

# We draw the standard deviation of the hidden extinction rates from an exponential distribution with mean H 
# (so that we expect the 95% interval of the hidden extinction rate to span 1 order of magnitude).
extinction_hidden_sd ~ dnExponential( 1.0 / H )
moves.append( mvScale(extinction_hidden_sd, lambda=1, tune=true, weight=2.0) )

# Create a deterministic variable for the hidden extinction rate categories
# using a discretized lognormal distribution (the N-quantiles of it)
extinction_hidden_unormalized := fnDiscretizeDistribution( dnLognormal(ln_extinction_hidden_mean, extinction_hidden_sd), NUM_HIDDEN )

# We normalize the hidden extinction rates by dividing the rates with the main 
# (so the mean of the normalized rates equals to 1.0)
extinction_hidden := extinction_hidden_unormalized / mean(extinction_hidden_unormalized)



### Third, we create the speciation and extinction rates for the observed states
for (i in 1:NUM_STATES) {
    
    ### Create a loguniform distributed variable for the speciation rate
    speciation_observed[i] ~ dnLoguniform( 1E-6, 1E2)
    speciation_observed[i].setValue( (NUM_TOTAL_SPECIES-2) / tree_length )
    moves.append( mvScale(speciation_observed[i],lambda=1.0,tune=true,weight=3.0) )

    ### Create a loguniform distributed variable for the speciation rate
    extinction_observed[i] ~ dnLoguniform( 1E-6, 1E2)
    extinction_observed[i].setValue( speciation_observed[i] / 10.0 )
    moves.append( mvScale(extinction_observed[i],lambda=1.0,tune=true,weight=3.0) )

}



### Finally, we put the observed and hidden rates together
for (j in 1:NUM_HIDDEN) {
    for (i in 1:NUM_STATES) {
        index = i+(j*NUM_STATES)-NUM_STATES
        speciation[index] := speciation_observed[i] * speciation_hidden[j]
        extinction[index] := extinction_observed[i] * extinction_hidden[j]
    }
}


#########################################################
# Set up the transition rate matrix for observed states #
#########################################################

# Each transition rate between observed states are drawn
# from an exponential distribution with a mean of 10
# character state transitions over the tree. 
rate_pr := observed_phylogeny.treeLength() / 10
for ( i in 1:(NUM_STATES*(NUM_STATES-1)) ) {
    transition_rates[i] ~ dnExp(rate_pr)
    moves.append( mvScale(transition_rates[i],lambda=0.50,tune=true,weight=3.0) )
}


#########################################################
# Set up the transition rate matrix for hidden states #
#########################################################

# We assume the transitions among the hidden states
# are all equal and drawn from an exponential distriubtion.
hidden_rate ~ dnExponential(rate_pr)
moves.append( mvScale(hidden_rate,lambda=0.5,tune=true,weight=5) )

for (i in 1:(NUM_HIDDEN * (NUM_HIDDEN - 1))) {
    R[i] := hidden_rate
}

######################################################################
# Create the rate matrix for the combined observed and hidden states #
######################################################################
rate_matrix := fnHiddenStateRateMatrix(transition_rates, R, rescaled=false)



#####################################
# Set up the root state frequencies #
#####################################

### Create a constant variable with the prior probabilities of each rate category at the root.
rate_category_prior ~ dnDirichlet( rep(1,NUM_RATES) )
moves.append( mvBetaSimplex(rate_category_prior,tune=true,weight=2) )
moves.append( mvDirichletSimplex(rate_category_prior,tune=true,weight=2) )


### Create the fix parameter for the age of the root set to the observed age
root <- observed_phylogeny.rootAge()


### rho is the probability of sampling species at the present
### fix this to 233/367
rho <- observed_phylogeny.ntips()/367


#### construct a variable for the tree drawn from a birth death process
timetree ~ dnCDBDP( rootAge           = root,
                    speciationRates   = speciation,
                    extinctionRates   = extinction, 
                    Q                 = rate_matrix,
                    delta             = 1.0,
                    pi                = rate_category_prior,
                    rho               = rho,
                    condition         = "survival" )

### clamp the model with the "observed" tree
timetree.clamp( observed_phylogeny )
timetree.clampCharData( data_exp )




#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rate_matrix)

### set up the monitors that will output parameter values to file and screen 
monitors.append( mnModel(filename="output/primates_HiSSE_2.log", printgen=1) )
#monitors.append( mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="NaturalNumbers", printgen=1, withTips=true, withStartStates=false, filename="output/anc_states_primates_HiSSE_2.log") )
monitors.append( mnStochasticCharacterMap(cdbdp=timetree, printgen=10, filename="output/stoch_char_map_primates_HiSSE.log", include_simmap=true) )
monitors.append( mnScreen(printgen=10, speciation_observed, extinction_observed) )


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, moveschedule="random", combine="mixed")

### run the MCMC
mymcmc.run(generations=2500, tuningInterval=100, underPrior=!TRUE)


##############################
# Summarize ancestral states #
##############################


burnin=25
n_time_slices = 500

# read in the sampled character histories
anc_states = readAncestralStateTrace("output/stoch_char_map_primates_HiSSE.log")

summarizeCharacterMaps(anc_states, observed_phylogeny, file="output/events.tsv", burnin=0.1)


# make summary tree
char_map_tree = characterMapTree(tree=observed_phylogeny, 
                 ancestral_state_trace_vector=anc_states, 
                 character_file="output/marginal_character.tree", 
                 posterior_file="output/marginal_posterior.tree", 
                 burnin=burnin, 
                 num_time_slices=n_time_slices)

q()