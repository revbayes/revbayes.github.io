########################################################
#
# RevBayes Example: Molecular dating
# 
# This file: Runs the full MCMC for exercise 3
#
# authors: Rachel C. M. Warnock, Sebastian Hoehna
#
########################################################

############################################################
#
# Exercise 3: Estimating speciation times using node dating
#
############################################################

#######################
# Reading in the Data #
#######################

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
cytb <- readDiscreteCharacterData("data/bears_cytb.nex")

## helpers
n_taxa <- cytb.size()
taxa <- cytb.taxa()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

# Load the model files

source("scripts/tree_BD_nodedate.Rev") # BD tree prior + node calibrations

source("scripts/clock_relaxed_exponential.Rev") # Relaxed exponential clock model

source("scripts/sub_GTRG.Rev") # Molecular substitution model (GTR+G)

########
# MCMC #
########

# initialize the model object #
mymodel = model(sf)	

# ignore phySeq node to run with fossil data only
mymodel.ignoreData(phySeq) 		# obs_age_ursinae is retained

# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/bears_nodedate_only_fossil_data.log", printgen=10)	)

# 2. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=10, extant_mrca, diversification) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=20000, tuningInterval=200)

# Quit RevBayes #
q()
