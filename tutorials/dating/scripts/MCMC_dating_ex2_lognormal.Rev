########################################################
#
# RevBayes Example: Molecular dating
#
# This file: Runs the full MCMC for exercise 2
#
# authors: Rachel C. M. Warnock, Sebastian Hoehna
#
########################################################

##################################################################
#
# Exercise 2: the The uncorrelated lognormal relaxed clock model
#
##################################################################

#######################
# Reading in the Data #
#######################

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
cytb <- readDiscreteCharacterData("data/bears_cytb.nex")

## helpers
n_taxa <- cytb.size()
taxa <- cytb.taxa()


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

# Load the model files

source("scripts/tree_BD.Rev") # BD tree prior

#source("scripts/clock_relaxed_exponential.Rev") # Relaxed exponential clock model
source("scripts/clock_relaxed_lognormal.Rev") # Relaxed exponential clock model

source("scripts/sub_GTRG.Rev") # Molecular substitution model (GTR+G)

########
# MCMC #
########

# initialize the model object #
mymodel = model(sf)

# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/bears_relaxed_lognormal.log", printgen=10) )

# 2. the tree #
monitors.append( mnFile(filename="output/bears_relaxed_lognormal.trees", printgen=10, timetree) )

monitors.append( mnExtNewick(filename="output/bears_relaxed_lognormal_ext.trees", printgen=10, tree=timetree, branch_rates) )

# 3. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=100, extant_mrca, diversification) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=50000, tuningInterval=200)


########################
# Summarizing the tree #
########################

# Read the trace file #
trace = readTreeTrace("output/bears_relaxed_lognormal.trees")

# Maximum clade credibility tree #
mccTree(trace, file="output/bears_relaxed_lognormal.mcc.tre" )

# Read the trace file #
trace = readTreeTrace("output/bears_relaxed_lognormal_ext.trees")

# Maximum clade credibility tree #
mapTree(trace, file="output/bears_relaxed_lognormal_ext.map.tre" )

# Quit RevBayes #
q()
