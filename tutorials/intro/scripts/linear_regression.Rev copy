
#
# Bayesian linear regression example
#
# by Will Freyman
#


x_obs <- readDataDelimitedFile(file="data/x.csv", header=FALSE, delimiter=",")[1]
y_obs <- readDataDelimitedFile(file="data/y.csv", header=FALSE, delimiter=",")[1]

beta ~ dnNormal(0, 1)
alpha ~ dnNormal(0, 1)
sigma ~ dnExponential(1)

for (i in 1:x_obs.size()) {
    mu_y[i] := (beta * x_obs[i]) + alpha
    y[i] ~ dnNormal(mu_y[i], sigma)
    y[i].clamp(y_obs[i])
}

moves[1] = mvSlide(beta, delta=1, weight=5)
moves[2] = mvSlide(alpha, delta=1, weight=5)
moves[3] = mvSlide(sigma, delta=1, weight=5)

monitors[1] = mnScreen(printgen=10000)
monitors[2] = mnModel("output/linear_regression.log")

mymodel = model(beta)

mymcmc = mcmc(mymodel, moves, monitors, nruns=2)
mymcmc.run(100000,tuningInterval=100)

q()
